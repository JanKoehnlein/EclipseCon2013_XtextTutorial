grammar org.eclipse.xtext.tutorial.survey.Survey with org.eclipse.xtext.common.Terminals

generate survey "http://www.eclipse.org/xtext/tutorial/survey/Survey"

Survey:
	'survey'
	name=ID
	title=STRING
	pages+=Page*;

Page:
	'page' name=ID '('
		questions+=Question*
		// A page can specify the next page. We use a cross-reference here
		// ('[]') as the referred page is actually defined somewhere else.  
		('->' next=[Page])?
	')';

Question:
	FreeTextQuestion | ChoiceQuestion;

FreeTextQuestion:
	'text' name=ID text=STRING;

ChoiceQuestion:
	(single?='single')? 'choice' name=ID text=STRING '('
	  choices+=Choice+
	')';

Choice:
	(freetext?='text')? (name=ID)? text=STRING;